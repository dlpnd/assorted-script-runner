; Which scan code to use: https://geckwiki.com/index.php?title=DirectX_Scancodes

; Which key to enable True Iron Sights. 
; Defaul: 264 (Scroll Up)
int iSightsKeyCode = 264

; Which key to enable Hip Fire. 
; Default: 265 (Scroll Down)
int iHipfireKeyCode = 265

; Which key you use for Aiming.
; Default: 257 (Right Click)
int iAimingKeyCode = 257

; Switch instantly, or wait until you release the aim key, and aim again.
; Default: 1
int iInstantlySwitch = 1

; How many frames to wait between toggling the modes. 
; Only used if iInstantlySwitch is set to 1
; Default: 15
int iFramesToWait = 15

array_var aWeaponBlockList = GetINISection "Weapons" "DLpnd\tst_weaps.ini"

SetOnKeyDownEventHandler (begin function { int iKeyCode }
    if eval (MenuMode || !IsPC1stPerson || Ar_HasKey aWeaponBlockList (GetEditorId(Player.GetEquippedObject 5)))
        return
    endif

    if eval (iInstantlySwitch && Player.IsAiming && (Player.AuxVarGetFLT "*iLastKeyCode") != iSightsKeyCode)
        DisableKey iAimingKeyCode
        CallAfterFrames iFramesToWait ({} => EnableKey iAimingKeyCode)
    endif

    SetNumericIniSetting "bTrueIronSights:GamePlay" 1
    Player.AuxVarSetFLT "*iLastKeyCode" iSightsKeyCode 
end) 1 iSightsKeyCode

SetOnKeyDownEventHandler (begin function { int iKeyCode }
    if eval (MenuMode || !IsPC1stPerson || Ar_HasKey aWeaponBlockList (GetEditorId(Player.GetEquippedObject 5)))
        return
    endif

    if eval (iInstantlySwitch && Player.IsAiming && (Player.AuxVarGetFLT "*iLastKeyCode") != iHipfireKeyCode)
        DisableKey iAimingKeyCode
        CallAfterFrames iFramesToWait ({} => EnableKey iAimingKeyCode)
    endif

    SetNumericIniSetting "bTrueIronSights:GamePlay" 0
    Player.AuxVarSetFLT "*iLastKeyCode" iHipfireKeyCode
end) 1 iHipfireKeyCode

SetOnKeyDownEventHandler ({ int iKeyCode } => EnableKey iAimingKeyCode) 1 iAimingKeyCode